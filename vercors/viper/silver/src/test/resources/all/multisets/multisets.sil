method test01() {
  var B1: Multiset[Int]
  var B2: Multiset[Int]
  B1 := Multiset(1)
  B2 := Multiset(1, 1)

  //:: ExpectedOutput(assert.failed:assertion.false)
  assert B1 == B2
}

method test02() {
  var B: Multiset[Int] := Multiset(1,2,3,3,1)
  assert |B| == 5
  assert |Multiset[Int]()| == 0
}

method test03(B: Multiset[Int], n: Int, x: Int, y: Int)
  requires |B| == n
  requires (x in B) > 0
  requires (y in B) > 0
{
  var B1: Multiset[Int] := B setminus Multiset[Int]()
  assert (x in B1) > 0
  assert (y in B1) > 0
  assert |B1| == n
}

method test04a(B: Multiset[Int], n: Int, x: Int, y: Int)
  requires |B| == n
  requires (x in B) > 0
  requires (y in B) > 0
  requires x != y
{
  var B1: Multiset[Int] := B setminus Multiset(x)
  assert (y in B1) > 0
  assert |B1| == n - 1
  //:: ExpectedOutput(assert.failed:assertion.false)
  assert (x in B1) > 0
}

/* Copy of test04a with some lines commented */
method test04b(B: Multiset[Int], n: Int, x: Int, y: Int)
  requires |B| == n
  requires (x in B) > 0
//  requires (y in B) > 0
  requires x != y
{
  var B1: Multiset[Int] := B setminus Multiset(x)
  assert |B1| == n - 1

  //:: ExpectedOutput(assert.failed:assertion.false)
  assert (x in B1) > 0
}

method test05(B: Multiset[Int], n: Int, x: Int)
  requires |B| == n
{
  var B1: Multiset[Int] := B union B
  assert (x in B1) > 0 <==> (x in B) > 0
  assert |B1| == 2*n
}

method test06a(B: Multiset[Int], n: Int, x: Int, b:Bool)
  requires |B| == n
{
  var B1: Multiset[Int] := B intersection B

  // NOTE: cardinality for multisets has no strong axioms
  //:: ExpectedOutput(assert.failed:assertion.false)
  assert |B1| == n
}

method test06b(B: Multiset[Int], n: Int, x: Int, b:Bool)
  requires |B| == n
{
  var B1: Multiset[Int] := B intersection B
  // NOTE: cardinality for multisets has no strong axioms
  // But equality can be used to overcome this incompleteness
  assert B1 == B
  assert |B1| == n
}

method test07(B: Multiset[Int], n: Int, x: Int, y: Int)
  requires |B| == n
{
  var B1: Multiset[Int] := B union Multiset(x, y)
  assert (x in B1) > 0
  assert (y in B1) > 0
  assert |B1| == n + 2
}

method test08(B: Multiset[Int], n: Int, x: Int, y: Int)
  requires |B| == n
{
  var B1: Multiset[Int] := B intersection Multiset(x, y)
  assert (x in B1) > 0  <==> (x in B) > 0
  // NOTE: cardinality for multisets has no strong axioms
  //:: ExpectedOutput(assert.failed:assertion.false)
  assert |B1| <= 2
}

class Query {
  int serialNumber;

  constructor(int serialNumber) {
    this.serialNumber = serialNumber;
  }

  Query clone();
}

class Client {
  Query query, temp;
  boolean flag, flag1, flag2, flag3, flag4;

  ensures query != null;
  ensures flag == false;
  constructor(int n) {
    query = new Query(5);
    temp = new Query(-1);
    flag = false;
  }

  ensures \old(query) == query;
  ensures   flag1 && flag2 && flag3 && flag4  ==>  flag;
  ensures !(flag1 && flag2 && flag3 && flag4) ==> !flag;
  void checkFlags() {
    flag = flag1 && flag2 && flag3 && flag4;
  }
}

class Server {
  int status;
  boolean flag;
  Query query;

  pure int INITIAL() = 0;
  pure int STARTED() = 1;
  pure int COMMITTED() = 2;
  pure int ABORTED() = 3;

  ensures status == INITIAL();
  constructor(int n) {
    status = 0;
    flag = false;
    query = new Query(-1); // dummy
  }

  requires status == INITIAL() || status == ABORTED();
  ensures status == STARTED();
  void start() {
    status = 1;
  }

  requires status == STARTED();
  ensures status == COMMITTED();
  void `commit`() {
    status = 2;
  }

  requires status == STARTED();
  ensures status == ABORTED();
  void abort() {
    status = 3;
  }

  ensures \result == (status != COMMITTED());
  pure boolean isUncommitted() {
    return status != 2;
  }
}

pure boolean consistency(Client c, Server s1, Server s2, Server s3, Server s4) =
    (!c.flag ==> s1.isUncommitted()) &&
    (!c.flag ==> s2.isUncommitted()) &&
    (!c.flag ==> s3.isUncommitted()) &&
    (!c.flag ==> s4.isUncommitted());

choreography Main(int n) {
  endpoint c = Client(n);
  endpoint s1 = Server(n);
  endpoint s2 = Server(n);
  endpoint s3 = Server(n);
  endpoint s4 = Server(n);

  requires c.query != null;
  requires c.flag == false;
  requires s1.status == s1.INITIAL();
  requires s2.status == s2.INITIAL();
  requires s3.status == s3.INITIAL();
  requires s4.status == s4.INITIAL();
  requires (\chor consistency(c, s1, s2, s3, s4));
  ensures s1.status == s1.COMMITTED();
  ensures s2.status == s2.COMMITTED();
  ensures s3.status == s3.COMMITTED();
  ensures s4.status == s4.COMMITTED();
  run {
    loop_invariant c.query != null;
    loop_invariant s1.status == s1.INITIAL() || s1.status == s1.COMMITTED() || s1.status == s1.ABORTED();
    loop_invariant s2.status == s2.INITIAL() || s2.status == s2.COMMITTED() || s2.status == s2.ABORTED();
    loop_invariant s3.status == s3.INITIAL() || s3.status == s3.COMMITTED() || s3.status == s3.ABORTED();
    loop_invariant s4.status == s4.INITIAL() || s4.status == s4.COMMITTED() || s4.status == s4.ABORTED();
    loop_invariant (\chor consistency(c, s1, s2, s3, s4));
    while (!c.flag &&
           s1.isUncommitted() &&
           s2.isUncommitted() &&
           s3.isUncommitted() &&
           s4.isUncommitted()) {
      communicate s1.query <- c.query.clone();
      communicate s2.query <- c.query.clone();
      communicate s3.query <- c.query.clone();
      communicate s4.query <- c.query.clone();

      s1.start();
      s2.start();
      s3.start();
      s4.start();

      communicate c.flag1 <- s1.flag;
      communicate c.flag2 <- s2.flag;
      communicate c.flag3 <- s3.flag;
      communicate c.flag4 <- s4.flag;

      c.checkFlags();

      communicate s1.flag <- c.flag;
      communicate s2.flag <- c.flag;
      communicate s3.flag <- c.flag;
      communicate s4.flag <- c.flag;

      if (c.flag && s1.flag && s2.flag && s3.flag && s4.flag) {
          s1.`commit`();
          s2.`commit`();
          s3.`commit`();
          s4.`commit`();
      } else {
          s1.abort();
          s2.abort();
          s3.abort();
          s4.abort();
      }
    }
  }
}

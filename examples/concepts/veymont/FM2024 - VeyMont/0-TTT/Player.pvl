class Player {
	int c00, c01, c02, c10, c11, c12, c20, c21, c22;
	int myMark;
	Move move;
	boolean turn;
	
	
	requires tok == 0 || tok == 1;
	ensures c00 == -1 && c01 == -1 && c02 == -1 && 
			c10 == -1 && c11 == -1 && c12 == -1 && 
			c20 == -1 && c21 == -1 && c22 == -1;
	ensures myMark == tok;
	ensures myMark == 0 || myMark == 1;
	ensures turn == t;
	constructor(int tok, boolean t) {
		c00 = -1;
		c01 = -1;
		c02 = -1;
		c10 = -1;
		c11 = -1;
		c12 = -1;
		c20 = -1;
		c21 = -1;
		c22 = -1;
		myMark = tok;
		turn = t;
		move = new Move(0, 0, tok);
	}
	

	context 0 <= move.i && move.i <= 2;
	context 0 <= move.j && move.j <= 2;
	requires move.mark == 0 || move.mark == 1;
	ensures myMark == \old(myMark);
	// TODO (RR): removing this precondition is probably a bad idea! Correctness is no longer guaranteed, I think...
	// requires readMoveCell() == -1;
	ensures readMoveCell() == move.mark;
	ensures move.i == \old(move.i);
	ensures move.j == \old(move.j);
	ensures move.mark == \old(move.mark);
	ensures turn == \old(turn);
	ensures move.i != 0 || move.j != 0 ==> \old(c00) == c00;
	ensures move.i != 0 || move.j != 1 ==> \old(c01) == c01;
	ensures move.i != 0 || move.j != 2 ==> \old(c02) == c02;
	ensures move.i != 1 || move.j != 0 ==> \old(c10) == c10;
	ensures move.i != 1 || move.j != 1 ==> \old(c11) == c11;
	ensures move.i != 1 || move.j != 2 ==> \old(c12) == c12;
	ensures move.i != 2 || move.j != 0 ==> \old(c20) == c20;
	ensures move.i != 2 || move.j != 1 ==> \old(c21) == c21;
	ensures move.i != 2 || move.j != 2 ==> \old(c22) == c22;			
	void doMove() {
		if(move.i == 0 && move.j == 0) {
			c00 = move.mark;
		} else if(move.i == 0 && move.j == 1) {
			c01 = move.mark;
		} else if(move.i == 0 && move.j == 2) {
			c02 = move.mark;
		} else if(move.i == 1 && move.j == 0) {
			c10 = move.mark;
		} else if(move.i == 1 && move.j == 1) {
			c11 = move.mark;
		} else if(move.i == 1 && move.j == 2) {
			c12 = move.mark;
		} else if(move.i == 2 && move.j == 0) {
			c20 = move.mark;
		} else if(move.i == 2 && move.j == 1) {
			c21 = move.mark;
		} else if(move.i == 2 && move.j == 2) {
			c22 = move.mark;
		}
	}

	requires 0 <= move.i && move.i <= 2;
	requires 0 <= move.j && move.j <= 2;
	pure int readMoveCell() =
		(move.i == 0 && move.j == 0) ? c00
		: (move.i == 0 && move.j == 1) ? c01
		: (move.i == 0 && move.j == 2) ? c02 
		: (move.i == 1 && move.j == 0) ? c10
		: (move.i == 1 && move.j == 1) ? c11
		: (move.i == 1 && move.j == 2) ? c12
		: (move.i == 2 && move.j == 0) ? c20
		: (move.i == 2 && move.j == 1) ? c21
		: c22;
		
	pure boolean boardFull() =
		c00 != -1 && c01 != -1 && c02 != -1 && 
		c10 != -1 && c11 != -1 && c12 != -1 && 
		c20 != -1 && c21 != -1 && c22  != -1;
		
	pure boolean boardWin() =
		c00 != -1 && c00 == c01 && c01 == c02 ||
		c10 != -1 && c10 == c11 && c11 == c12 ||
		c20 != -1 && c20 == c21 && c21 == c22 ||
		c00 != -1 && c00 == c10 && c10 == c20 ||
		c01 != -1 && c01 == c11 && c11 == c21 ||
		c02 != -1 && c02 == c12 && c12 == c22 ||
		c00 != -1 && c00 == c11 && c11 == c22 ||
		c02 != -1 && c02 == c11 && c11 == c20;
		
	pure boolean gameFinished() = boardFull() || boardWin();
	
	pure boolean equalBoard(Player other) =
		c00 == other.c00 && c01 == other.c01 && c02 == other.c02 && 
		c10 == other.c10 && c11 == other.c11 && c12 == other.c12 && 
		c20 == other.c20 && c21 == other.c21 && c22 == other.c22;

    ensures move != null;
	ensures 0 <= move.i && move.i <= 2;
	ensures 0 <= move.j && move.j <= 2;
	requires myMark == 0 || myMark == 1;
	ensures move.mark == myMark;
	ensures move.mark == 0 || move.mark == 1;
	ensures myMark == \old(myMark);
	ensures turn == \old(turn);
	requires !boardFull();
	ensures readMoveCell() == -1;
	ensures c00 == \old(c00) && c01 == \old(c01) && c02 == \old(c02) && 
		c10 == \old(c10) && c11 == \old(c11) && c12 == \old(c12) && 
		c20 == \old(c20) && c21 == \old(c21) && c22 == \old(c22);
	void createNewMove();

}
